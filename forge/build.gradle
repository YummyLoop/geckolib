configurations {
    shadow
}

//libsDirName = "../out" // Output location of jar instead of default build/libs

architectury {
    platformSetupLoomIde()
}

loom {
     useFabricMixin = true
}

apply from: "${rootDir}/repositories.gradle"
apply from: "${rootProject.project("common").projectDir}/shadow.gradle"

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modCompile "me.shedaniel:architectury:${rootProject.architectury_version}:forge"

    modCompileOnly "vazkii.patchouli:Patchouli:1.16.4-48"

    // Actual dependencies
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.0'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.0'
    implementation 'com.eliotlash.molang:molang:SNAPSHOT.12'
    implementation 'com.eliotlash.mclib:mclib:SNAPSHOT.12'

    // Shaded dependencies
    shadow group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.0'
    shadow group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.0'
    shadow 'com.eliotlash.molang:molang:SNAPSHOT.12'
    shadow 'com.eliotlash.mclib:mclib:SNAPSHOT.12'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

    test {
        useJUnitPlatform()
    }

    compileOnly(project(path: ":common")) {
        transitive = false
    }
    runtimeOnly(project(path: ":common", configuration: "transformDevelopmentForge")) {
        transitive = false
    }
    shadow(project(path: ":common", configuration: "transformProductionForge")) {
        transitive = false
    }

    // Extra dependencies
    // JEI https://www.curseforge.com/minecraft/mc-mods/jei
    modCompileOnly("mezz.jei:jei-1.16.4:7.6.1.71:api")
    modRuntime("mezz.jei:jei-1.16.4:7.6.1.71")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    configurations = [project.configurations.shadow]
    from sourceSets.main.allSource
    duplicatesStrategy = 'exclude'
    relocate 'com.eliotlash', 'software.bernie.shadowed.eliotlash'
    relocate 'com.fasterxml', 'software.bernie.shadowed.fasterxml'
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    classifier "forge"
}